var tmp[9]
var i

var _camera.heartbeat = 50

var camera.detect[5] = [0, 0, 0, 0, 0]
var camera.thing[60]

# Constant QUEUE = 7
var Q = 9

var Qid[9]       # task id
var Qtime[9]     # remaining time
var QspL[9]      # motor speed L
var QspR[9]      # motor speed R
var Qpc = 0      # program counter
var Qnx = 0      # next pc

var base = 800	 # base speed * 10
var speed = 0
var black = 300
var lost = 0
var lost_turn[2] = [200, -350]
#var lost_turn[2] = [70,  -140]  # 0 = arc right, 1 = arc left; tick=50 -> through 30 degrees
var lost_phase
var wayout.chkd[3] = [0, 0, 0]
var wayout.seen[3] = [0, 0, 0]

var state = 0    # 0 = follow track

call math.fill(Qid, 0)
call math.fill(Qtime,0)
call math.fill(QspL,0)
call math.fill(QspR,0)

mic.threshold = 12

# Main event

sub doit
	# -- specialized: add default behavior here
	if state == 0 then
		callsub camera_detect
		callsub state0_follow_track
	elseif state == 1 then
		callsub state1_look_around
	end

# Following

sub state0_follow_track
	if speed > 0 then
		if  prox.ground.delta[0] < black and prox.ground.delta[1] < black then
			motor.left.target = speed / 10
			motor.right.target = speed / 10
			callsub not_lost
		elseif  prox.ground.delta[0] < black and prox.ground.delta[1] >= black then
			motor.left.target = speed / 10
			motor.right.target = 6 * (speed / 10)
			callsub not_lost
		elseif  prox.ground.delta[0] >= black and prox.ground.delta[1] < black then
			motor.left.target = 6 * (speed / 10)
			motor.right.target = speed / 10
			callsub not_lost
		elseif  prox.ground.delta[0] >= black and prox.ground.delta[1] >= black then
			lost = lost + 1
			if lost < 5 then
				# keep moving
			elseif lost < 200 then
				if wayout.chkd[1] == 0 then
					state = 1
					lost = lost + 70
					callsub do_look_around
				else
					lost_phase = (lost / 100) % 2
					motor.left.target = lost_turn[lost_phase]
					motor.right.target = lost_turn[1 - lost_phase]
				end
			else
				callsub do_stop
			end
		end
	else
		motor.left.target = 0
		motor.right.target = 0
	end
	leds.bottom.left = [0, 0,0]
	leds.bottom.right = [0, 0, 0]

sub state1_look_around
	if wayout.chkd[0] > 0 and wayout.chkd[2] > 0 then
		wayout.chkd[1] = 99
		# we have checked both sides,  so choose which way to turn
		if wayout.seen[0] < black and wayout.seen[2] < black then
			# seen both; choose randomly
			call math.rand(lost_phase)
			call math.clamp(lost_phase,  (lost_phase/320)+ 50, -1,1)  # bias right 2:1
			lost_phase = (lost_phase + 1) / 2
		elseif wayout.seen[0] < black then
			lost_phase = 1  # seen left,  turn left
		elseif wayout.seen[2] < black then
			lost_phase = 0  # seen right,  turn right
		else # very lost
			tmp[0:3] = [101,  2*180,  137, -137]  # 180 degrees
			callsub motion_add
			callsub do_normal
			state = 0
			return
		end

		state = 0

		motor.left.target = 3 * lost_turn[lost_phase]
		motor.right.target = 3 * lost_turn[1 - lost_phase]
	else
		# still checking,  wait
	end

sub not_lost
	wayout.chkd = [0,0,0]
	wayout.seen = [0,0,0]
	lost = 0

# Inputs

onevent prox
	call math.dot(tmp[0], prox.horizontal[0:4], [1,1,1,1,1], 1)
	if tmp[0] > 500 then
		callsub do_stop
	end

onevent tap
	event.args[0] = [87]
	callsub do_command

onevent rc5
	event.args[0] = [rc5.command]
	callsub do_command

onevent command
	callsub do_command

onevent button.forward
	event.args[0] = [53]
	callsub do_command

onevent button.center
	event.args[0] = [87]
	callsub do_command

# -- specialized
onevent camera.detect
	_camera.heartbeat = 50 + (((_camera.heartbeat % 10) + 1) % 7)
	call leds.temperature(0, 4 - abs((_camera.heartbeat % 10) % 7 - 3))
	if state > 0 then
		callsub camera_detect
	end

sub camera_detect
	# -- specialized: add detection here
	if speed > 0 then
		if camera.thing[14*4] > 50 and speed < 2000 then
			# straightaway
			motor.right.target = motor.left.target
			callsub do_faster
		elseif camera.thing[12*4] + camera.thing[13*4] > 50 then
			# turn ahead
			callsub do_slow
		elseif camera.thing[2*4] > 50 and camera.thing[2*4 + 3] < 500 and camera.thing[2*4 + 3] < 150 then
			# stop sign
			callsub do_long_stop
		elseif camera.thing[1*4] > 50 and camera.thing[1*4 + 3] < 500 and camera.thing[1*4 + 3] < 150 then
			# zebra sign
			callsub do_short_stop
		elseif camera.thing[0*4] > 50 and camera.thing[0*4 + 3] < 250 and abs(camera.thing[0*4 + 2]) < 300 then
			# parking sign
			callsub do_stop
		else
			leds.circle[5:6] = [0,0]
		end
	end

# commands
sub do_command
	# -- standard
	if event.args[0] == 87 then # stop
		call sound.system(1)
		callsub do_stop
		state = 0
	elseif event.args[0] == 53 then # go
		call sound.system(0)
		callsub do_normal
		state = 0
	elseif event.args[0] == 0 then # 0 = sweep
		callsub do_sweep
	elseif event.args[0] == 80 then # forward
		callsub do_forward
	elseif event.args[0] == 81 then # backward
		callsub do_backward
	elseif event.args[0] == 86 then # right
		callsub do_right
	elseif event.args[0] == 85 then # left
		callsub do_left
	elseif event.args[0] == 16 then # plus
		callsub do_faster
	elseif event.args[0] == 17 then # minus
		callsub do_normal
	elseif event.args[0] == 7 then # 7 = stop sign
		callsub do_long_stop
	elseif event.args[0] == 9 then # 9 = zebra
		callsub do_short_stop
	end

sub do_stop
	speed = 0
	callsub motion_reset

sub do_normal
	speed = base
	call sound.system(2)
	motor.left.target = speed
	motor.right.target = speed
	leds.bottom.left = [0, 0, 0]
	leds.bottom.right = [0, 0, 0]
	lost = 0

sub do_slow
	speed = base
	call sound.system(2)
	if abs(motor.right.target) > 20 then
		call math.muldiv(i, base, 100, abs(motor.left.target))
		call math.muldiv(tmp[0:1], [motor.left.target, motor.right.target], [i, i], [100, 100])
		call math.clamp(tmp[0:1], tmp[0:1], [-150,-150], [150,150])
		motor.left.target = tmp[0]
		motor.right.target = tmp[1]
	end

sub do_forward
	tmp[0:3] = [3, 100, 128, 128]  # 40 mm
	callsub motion_add

sub do_backward
	tmp[0:3] = [3, 100, -128,-128]  # 40 mm
	callsub motion_add

sub do_right
	tmp[0:3] = [2, 2*20, 137,-137]  # 20 degrees
	callsub motion_add

sub do_left
	tmp[0:3] = [2, 2 * 20, -137,137]  # 20 degrees
	callsub motion_add

sub do_faster
	# speed = speed * 12 / 10
	speed = speed + 1
	call math.clamp(speed, speed, 0, 2000)

sub do_sweep
	tmp[0:3] = [1,100,128,128]  # 40 mm
	callsub motion_add
	tmp[0:3] = [2,2 * 30,137,-137]  # 30 degrees
	callsub motion_add
	tmp[0:3] = [3,100,-128,-128]  # 40 mm
	callsub motion_add
	tmp[0:3] = [4,2 * 30,-137,137]  # 30 degrees
	callsub motion_add
	tmp[0:3] = [5,60,0,0]  # wait 600 ms
	callsub motion_add

# -- specialized: add commands here

sub do_long_stop
	tmp[0:3] = [5,300,0,0]
	callsub motion_add
	leds.bottom.left = [200, 0, 0]
	leds.bottom.right = [200, 0, 0]

sub do_short_stop
	tmp[0:3] = [5,100,0,0]
	callsub motion_add
	leds.bottom.left = [200, 0, 0]
	leds.bottom.right = [200, 0, 0]

sub do_look_around
	wayout.chkd = [0,0,0]
	wayout.seen = [2000,0,2000]

	tmp[0:3] = [60,  66,  -150,150]  # -30 degrees
	callsub motion_add
	tmp[0:3] = [61,  50,  0,0]  # wait 500 ms
	callsub motion_add
	tmp[0:3] = [62,  132,  150,-150]  # +60 degrees
	callsub motion_add
	tmp[0:3] = [63,  50,  0,0]  # wait 500 ms
	callsub motion_add
	tmp[0:3] = [64,  66,  -150,150]  # -30 degrees
	callsub motion_add
	# tmp[0:3] = [65,  50,  0,0]  # wait 500 ms
	# callsub motion_add

# Standard

# Motion queue

onevent motor # loop runs at 100 Hz
	if Qtime[Qpc] > 0 then
		# start new motion
		Qtime[Qpc] = 0 - Qtime[Qpc] # mark as current by setting negative value
	end
	if Qtime[Qpc] < 0 then
		# continue motion
		leds.top = [180,180,0]
		motor.left.target = QspL[Qpc]
		motor.right.target = QspR[Qpc]
		Qtime[Qpc] += 1
		if Qtime[Qpc] == 0 then
			Qid[Qpc] = 0
			Qpc = (Qpc+1) % Q
			if Qtime[Qpc] == 0 and Qpc == Qnx then
				motor.left.target = 0
				motor.right.target = 0
			end
		end
	end
	if Qtime[Qpc] == 0 and Qpc != Qnx then
		# scan forward in the queue
		Qpc = (Qpc+1) % Q
	end
	callsub run_clock
	callsub update_leds
	callsub update_wayout
	# if queue is empty do behavior
	if Qnx == Qpc then
		callsub doit
	end

sub update_leds
	if  speed == 0 then
		leds.top = [200,0,0]
	else
		leds.top = [0,200,0]
	end
	if state == 1 then
		leds.top = [0,200,200]
	end
	# leds.buttons show queue
	call math.fill(tmp,0)
	tmp[Qnx]=1
	tmp[Qpc]=4
	call leds.buttons(tmp[0],tmp[1],tmp[2],tmp[3])
	# leds.circle show speed
	call math.fill(leds.circle, 0)
	leds.circle[(abs(speed - 400) / 300) % 8] = 20

sub update_wayout
	if  state != 1 then
		return
	end
	tmp[0:2] = wayout.seen[0:2]
	if motor.right.target - motor.left.target > 20 and abs(Qtime[Qpc]) < 6 then
		call math.min(wayout.seen[0], prox.ground.delta[0], wayout.seen[0])
		call math.min(wayout.seen[0], prox.ground.delta[1], wayout.seen[0])
		if wayout.seen[0] < tmp[0] then
			wayout.chkd[0] = wayout.chkd[0] + 1
		end
	elseif motor.left.target - motor.right.target > 20 and abs(Qtime[Qpc]) < 6 then
		call math.min(wayout.seen[2], prox.ground.delta[0], wayout.seen[2])
		call math.min(wayout.seen[2], prox.ground.delta[1], wayout.seen[2])
		if wayout.seen[2] < tmp[2] then
			wayout.chkd[2] = wayout.chkd[2] + 1
		end
	end

# Add a task to the queue [ Qid Qtime QspL QspR ]
sub motion_add
	if (Qnx != Qpc or (Qnx == Qpc and Qtime[Qpc] == 0)) and Qid[0]!=tmp[0] and Qid[1]!=tmp[0] and Qid[2]!=tmp[0] and Qid[3]!=tmp[0] then
		Qid[Qnx]   = tmp[0]
		Qtime[Qnx] = tmp[1]
		QspL[Qnx]  = tmp[2]
		QspR[Qnx]  = tmp[3]
		Qnx = (Qnx+1) % Q
	# else silently ignore
	end

# Reset the motion queue
sub motion_reset
	motor.left.target = 0
	motor.right.target = 0
	call math.fill(Qid,0)
	call math.fill(Qtime,0)
	call math.fill(QspL,0)
	call math.fill(QspR,0)
	call math.fill(Qpc,0)
	call math.fill(Qnx,0)

# Animate the clock face based on Qtime[Qpc]
sub run_clock
	call math.fill(leds.circle, 0)
	if Qtime[Qpc] > 0 and motor.left.target == 0 and motor.right.target == 0 then
		leds.circle[abs(Qtime[Qpc]/25) % 8] = 30
	end
